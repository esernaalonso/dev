/*
//////////////////////////////////////////////////////////

ackPushPull 1.1
10/13/07					
Aaron Koressel				
www.aaronkoressel.com

Pushes or pulls animation in the graph editor.
Scales selected keys by some factor towards or away from
a pivot point.

ackSetup is required to change script settings.

One of three pivot points will be used
depending on what mode is set in the setup window:

PARAMETERS (via ackSetup):
 Pivot From:
* Adjacent Left (Multi Pivot): uses the adjacent left key per curve
as a pivot point.  If there is more than one curve selected there 
will be multiple pivot points - each curve independently looking
to the left adjacent key.

* Adjacent Right (Multi Pivot): same as above but to the right.

* Last Selected Key (Single Pivot): uses the last single key selected
of all curves as the pivot point.  There is only one pivot point and
all curves pivot from this key.
 
 Scale Factor:
How much to scale selected keys by.

SYNTAX:
ackPushPull "push" "first" "individual" 0.9;
ackPushPull "pull" "first" "individual" 0.9;
ackPushPull "snap" "first" "individual" 0.0;
ackPushPull "snap" "last" "individual" 0.0;

EXAMPLE:
Assign this commnad to "Alt /":  ackPushPull "pull" "first" "individual" 0.9;
Assign this commnad to "Alt *":  ackPushPull "push" "first" "individual" 0.9;
Assign this commnad to "Alt -":  ackPushPull "snap" "first" "individual" 0.0;
Assign this commnad to "Alt +":  ackPushPull "snap" "last" "individual" 0.0;

CHANGELOG:
2015/07/06		1.2
ackSetup compliant

7/08/2007/07/08			1.0
First version.
//////////////////////////////////////////////////////////
*/

//$ackSetup_pivot = "left";
//$ackSetup_affect = "individual";
//$ackSetup_scaleFactor = .9;

global proc ackPushPull(string $cmd, string $pivot, string $affect, float $scale_factor){
	//only run if at least one key is selected
	$keyCount = `keyframe -an keys -q -keyframeCount`;
	if ($keyCount != 0) {

		//set default value if doesn't exist
		if ($pivot == ""){
			$pivot = "first";
		}
		if ($affect == ""){
			$affect = "individual";
		}
		if ($scale_factor <= .0001){
			$scale_factor = .9;
		}

		float $pivotTime;
		float $pivotValue[];

		if ($pivot == "last"){
			$pivotValue = `keyframe -lastSelected -q -valueChange`;	
		}			

		//loop over selected curves and process independently
		string $selectedCurves[] = `keyframe -selected -q -name`;
		for ($c = 0; $c < size($selectedCurves); $c++){
			//channel to use for this pass
			$channel = $selectedCurves[$c];

			//get array of key times in selection (to find closest match key)
			$timeArray = `keyframe -selected -q -timeChange $channel`;

			//get pivot point

			switch ($pivot){
				case "first":
					$pivotTime = `findKeyframe -which "previous" -time $timeArray[0] $channel`;
					$pivotValue = `keyframe -time $pivotTime -q -valueChange $channel`;
					break;
				case "last":
					int $lastKey = size($timeArray) - 1;
					$pivotTime = `findKeyframe -which "next" -time $timeArray[$lastKey] $channel`;
					$pivotValue = `keyframe -time $pivotTime -q -valueChange $channel`;
					break;
			}


			//scale selected keys
			$selected = `keyframe -selected -q -indexValue $channel`;
			if ($cmd == "push"){
				for ($i = 0; $i < size($selected); $i++){
					scaleKey -index $selected[$i] -valuePivot $pivotValue[0] -valueScale (1/$scale_factor) $channel;
				}
			}else if ($cmd == "pull"){
				for ($i = 0; $i < size($selected); $i++){
					scaleKey -index $selected[$i] -valuePivot $pivotValue[0] -valueScale ($scale_factor) $channel;
				}
			}else if ($cmd == "snap"){
				for ($i = 0; $i < size($selected); $i++){
					scaleKey -index $selected[$i] -valuePivot $pivotValue[0] -valueScale 0 $channel;
				}
			}

		}	//loop curves
	}	//chk key selection
}

//EXAMPLES
//ackPushPull "push" "first" "individual" 0.9;
//ackPushPull "pull" "first" "individual" 0.9;
//ackPushPull "snap" "first" "individual" 0.0;
//ackPushPull "snap" "last" "individual" 0.0;